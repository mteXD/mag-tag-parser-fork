# Context free grammar for the policy files. Use the following website
# to check for corectness: http://mdaines.github.io/grammophone/#

SOURCE -> DECLS .

DECLS -> DECL DECLREST .

DECLREST -> DECL DECLREST .
DECLREST -> .

DECL -> TOPOLOGY .
DECL -> AWARE .
DECL -> PG .

TOPOLOGY -> topology id ":" TOPOLOGYREST .
AWARE -> aware id ":" TOPOLOGYREST .

TOPOLOGYREST -> basic "{" BASIC "}" .
TOPOLOGYREST -> linear LIN .
TOPOLOGYREST -> expr EXPR .

BASIC -> EDGE EDGEREST .

EDGE -> str arrow str .

EDGEREST -> comma EDGE EDGEREST .
EDGEREST -> .

LIN -> str LINREST .

LINREST -> comma LIN .
LINREST -> .

EXPR -> SUM .

SUM -> MUL SUM2 .

SUM2 -> "+" MUL SUM2 .
SUM2 -> .

MUL -> ELEM MUL2 .

MUL2 -> "*" ELEM MUL2 .
MUL2 -> .

ELEM -> id .
ELEM -> "(" SUM ")" .

PG -> pg id "{" PGREST "}" .

PGREST -> file ":" str tag "=" str .
